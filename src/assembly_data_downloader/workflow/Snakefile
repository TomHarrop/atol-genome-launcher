#!/usr/bin/env python3

from yaml_manifest import Manifest


def get_download_params(wildcards):
    my_file = manifest.reads.get(wildcards.filename)
    lanes = my_file.lanes_for_read(wildcards.read_number)
    lane = next(lf for lf in lanes if lf.lane_number == wildcards.lane_number)
    return {"url": lane.url, "md5sum": lane.md5sum}


def get_paired_end_lanes(wildcards):
    my_file = manifest.reads.get(wildcards.filename)
    lanes = my_file.lanes_for_read(wildcards.read_number)

    return [
        Path(
            download_dir,
            wildcards.data_type,
            wildcards.filename,
            wildcards.read_number,
            lf.lane_number,
            f"reads.{lf.file_ext}",
        )
        for lf in lanes
    ]


def get_single_end_lanes(wildcards):
    my_file = manifest.reads.get(wildcards.filename)
    lanes = my_file.lanes_for_read("single_end")

    return [
        Path(
            download_dir,
            wildcards.data_type,
            wildcards.filename,
            "single_end",
            lf.lane_number,
            f"reads.{lf.file_ext}",
        )
        for lf in lanes
    ]


# Configuration
manifest = Manifest.from_yaml(config.get("manifest_file"))
download_dir = manifest.get_dir("downloads")
logs_dir = manifest.get_stage_logs("raw")

# Wildcard constraints
all_read_numbers = ["r1", "r2", "single_end"]


wildcard_constraints:
    data_type="|".join(re.escape(x) for x in manifest.all_data_types),
    filename="|".join(re.escape(x) for x in manifest.reads.names),
    read_number="|".join(all_read_numbers),
    lane_number="|".join(re.escape(x) for x in manifest.all_lane_numbers),
    extension="|".join(re.escape(x) for x in manifest.all_extensions),


# Workflow
rule target:
    input:
        manifest.reads.flat_paths("raw"),


rule collect_single_end_files:
    input:
        get_single_end_lanes,
    output:
        Path(download_dir, "{data_type}", "{filename}.{extension}"),
    shell:
        "cat {input} > {output}"


rule collect_paired_end_files:
    input:
        get_paired_end_lanes,
    output:
        Path(download_dir, "{data_type}", "{filename}.{read_number}.{extension}"),
    shell:
        "cat {input} > {output}"


rule download_file:
    output:
        read_file=temp(
            Path(
                download_dir,
                "{data_type}",
                "{filename}",
                "{read_number}",
                "{lane_number}",
                "reads.{extension}",
            )
        ),
        check_file=temp(
            Path(
                download_dir,
                "{data_type}",
                "{filename}",
                "{read_number}",
                "{lane_number}",
                "reads.{extension}.check.txt",
            )
        ),
    log:
        Path(
            logs_dir,
            "download_file",
            "{data_type}",
            "{filename}.{read_number}.{lane_number}.{extension}.log",
        ),
    params:
        params=get_download_params,
    retries: 3
    shell:
        "bpa-file-downloader "
        "--file_checksum {params.params[md5sum]} "
        "{params.params[url]} "
        "{output.read_file} "
        "&> {log}"
